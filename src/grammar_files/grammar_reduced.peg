comment     = r"#.*"
program     = (statement / comment)* EOF
statement   = ("quit" / expr_stat) ";"
expr_stat   = init / assignment / definition / return / expr
expr        = plug_expr / simp_expr / compound_expr
definition  = identifier ":" type
assignment  = (record_access / list_access / identifier) "=" expr
            / (record_access / list_access / identifier) "=" def_record
init        = identifier ":" type "=" expr
            / identifier ":" "record" "=" def_record
return      = "return" expr
plug_expr   = simp_expr "->" plug_expr
            / simp_expr "->" simp_expr
simp_expr   = term
            / binary_expr
            / unary_expr
unary_expr  = unary_op term
binary_expr = binary_op term term
term        = call
            / record_access
            / list_access
            / literal
            / identifier
            / binary_expr
            / unary_expr
            / "(" simp_expr ")"
compound_expr   = lambda
                / lit_list
                / lit_record
type        = "int" / "float" / "byte" / "bool" / "record" / list / "process"
            / identifier
list        = "[" type "]"
unary_op    = "~" / "-"
            / un_bool_op
un_bool_op  = "not"
binary_op   = bin_math_op
            / bin_bool_op
bin_math_op = "@" / "&" / "|" / "^" / "+" / "-" / "*" / "/" / "%"
            / "<" / "<=" / "==" / "!=" / ">=" / ">"
bin_bool_op     = "and" / "or"
call        = identifier "(" (expr ("," expr )* )? ")"
record_access   = identifier "." identifier
                / identifier "." record_access
list_access     = identifier "[" simp_expr "]"
literal     = lit_byte / lit_float / lit_int / lit_bool
lit_list    = lit_str
            / "[" identifier? ("," identifier )* "]"
            / "[" expr ("," expr)* "]"
def_record  = "(" (definition / init) ("," (definition / init) )* ")"
lit_record  = "(" expr ("," expr)* ")"
lit_byte    = r"0[xX][0-9a-fA-F]+" / r"'([^']|\\')?'"
lit_int     = r"-?[0-9]+"
lit_float   = r"-?[0-9]*\.[0-9]+"
lit_bool    = r"true|false"
lit_str     = r'(?<!\\)".*?(?<!\\)"'
lambda      = lmbda_param lmbda_ret? lmbda_block
lmbda_param = "(" definition? ("," definition )* ")"
lmbda_ret   = "->" type
            / "->" "(" type? ("," type)* ")"
lmbda_block = "{" statement* "}"
identifier  = r"[a-zA-Z_]\w*"